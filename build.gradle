buildscript {
    dependencies {
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.asciidoctor.convert' version '1.5.2'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()
}

dependencies {
    compile "io.dropwizard:dropwizard-core:$dropwizardVersion",
            "io.dropwizard:dropwizard-migrations:$dropwizardVersion",
            "io.dropwizard:dropwizard-jdbi:$dropwizardVersion",
            'com.h2database:h2:1.4.191'

    testCompile "io.dropwizard:dropwizard-testing:$dropwizardVersion"

}

test {
    testLogging.showStandardStreams = true
}

mainClassName = 'com.infopulse.dropwizard.ServerMainApplication'

asciidoctor {
    backends 'pdf'
    logDocuments = true
}

distributions {
    main {
        contents {
            from(asciidoctor) {
                include "*.pdf"
                into ""
            }
        }
    }
    src {
        contents {
            from('src')

            from('.') {
                include "*.properties"
                include "build.gradle"
                include "settings.gradle"
                include "gradle*/**"
            }
        }
    }
}

distZip.archiveName = "${rootProject.name}.zip"

srcDistZip.archiveName = "${rootProject.name}-src.zip"

